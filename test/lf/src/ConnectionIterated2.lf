target uC {
  platform: Native
}

reactor Src {
  output [2] out:int

  reaction(startup) -> out {=
    for (int i = 0; i < out_width; i++) {
      lf_set(out[i], i);
    }
  =}
}

reactor Dest(bank_idx:int=0) {
  input in:int
  reaction(in) {=
    printf("Bank %d received %d\n", self->bank_idx, in->value);
    validate(in->value == self->bank_idx % 2);
  =}
}

reactor Dest2 {
  input[2] in:int
  reaction(in) {=
    printf("Received %d and %d\n", in[0]->value, in[1]->value);
    validate(in[0]->value == 0);
    validate(in[1]->value == 1);
  =}
}

reactor Dest3(expected:int=2) {
  input in:int
  reaction(in) {=
    printf("received %d\n", in->value);
    validate(in->value == self->expected);
  =}
}

main reactor {
  src = new Src()
  src2 = new Src()
  dest = new[4] Dest()
  dest2 = new Dest2()
  dest3_1 = new Dest3(expected=0)
  dest3_2 = new Dest3(expected=1)
  (src.out, src2.out)+ -> dest.in, dest2.in, dest3_1.in, dest3_2.in
}