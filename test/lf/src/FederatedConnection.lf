target uC {
  platform: Native,
  logging: debug
}

reactor Src(id: int = 0) {
  output out: int
  input in: bool
  reaction(startup) -> out{=
    printf("Hello from Src!\n");
    lf_set(out, self->id);
  =}

  reaction(in) {=
    printf("Received message from Dst!");
    env->request_shutdown(env);
  =}
}

reactor Dst {
  input in: int
  output out: bool
  state check: bool = false
  reaction(startup) {=
    printf("Hello from Dst!\n");
  =}
  reaction(in) -> out {=
    printf("Received %d from Src\n", in->value);
    validate(in->value == 42);
    self->check = true;
    env->request_shutdown(env);
    lf_set(out, true);
  =}

  reaction(shutdown) {=
    validate(self->check);
  =}
}

federated reactor {
  r1 = new Src(id=42)
  r2 = new Dst()
  r1.out -> r2.in after 1sec; 
  r2.out -> r1.in after 1sec;
}
