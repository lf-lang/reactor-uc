// This tests delaying an array type. The array length has to be agreed upon by all reactors.
target uC {
  platform: Native,
  build-type: RelWithDebInfo
}

preamble {=
  #include "reactor-uc/reactor-c.h"
=}
reactor DelayPointer(delay: time = 100 msec) {
  input in: int[3]
  output out: int[3]
  logical action a: int[3]

  reaction(in) -> a {=
    lf_schedule(a, self->delay, in->value);
  =}

  reaction(a) -> out {=
    lf_set_array(out, a->value);
  =}
}

reactor Source {
  output out: int[3]

  reaction(startup) -> out {=
    // Dynamically allocate an output array of length 3.
    // Note that the receiving reactors need to know that the length is 3.
    // A better design is given at:
    // https://www.lf-lang.org/docs/handbook/target-language-details?target=c#dynamically-allocated-data
    int* array =  (int*)malloc(3 * sizeof(int));
    for (size_t i = 0; i < 3; i++) {
      array[i] = i;
    }
    lf_set_array(out, array);
  =}
}

// The scale parameter is just for testing.
reactor Print(scale: int = 1) {
  input in: int[3]

  reaction(in) {=
    int count = 0;     // For testing.
    bool failed = false; // For testing.
    printf("Received: [");
    for (int i = 0; i < 3; i++) {
      if (i > 0) printf(", ");
      printf("%d", in->value[i]);
      // For testing, check whether values match expectation.
      if (in->value[i] != self->scale * count) {
        failed = true;
      }
      count++;     // For testing.
    }
    printf("]\n");
    if (failed) {
      printf("ERROR: Value received by Print does not match expectation!\n");
      exit(1);
    }
  =}
}

main reactor DelayArray {
  s = new Source()
  d = new DelayPointer()
  p = new Print()
  s.out -> d.in
  d.out -> p.in
}
